# How many validation triples are affected by relations that have an overrepresented head ( > 50% of all mentions) in the training graph
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT (COUNT(*) AS ?affectedTestTriples) WHERE {
  GRAPH <http://bias.org/training-graph> {
    {
      SELECT ?head ?relation ?tripleCount WHERE {
    {
      SELECT ?relation (COUNT(*) AS ?tripleCount) WHERE { ?s ?relation ?o. }
      GROUP BY ?relation
    }
    ?head ?relation ?tail.
  
}
GROUP BY ?relation ?head ?tripleCount
HAVING (((COUNT(*)) / (xsd:float(?tripleCount))) > "0.5"^^xsd:decimal)
    }
  }
    GRAPH <http://bias.org/validation-graph> {
        ?h ?relation ?t .
    }
}
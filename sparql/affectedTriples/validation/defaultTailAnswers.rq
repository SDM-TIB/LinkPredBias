# How many test triples are affected by relations that tend to have a default answer in the tail (for > 50% of all head entities) in the training graph
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT (COUNT(*) AS ?affectedTestTriples) WHERE {
  GRAPH <http://bias.org/training-graph> {
    {
      SELECT distinct ?relation ?tripleCount WHERE {
    {
      SELECT ?relation (COUNT(DISTINCT ?head) AS ?headEntities) (COUNT(*) AS ?tripleCount) WHERE { ?head ?relation ?tail. }
      GROUP BY ?relation
    }
    ?h ?relation ?option.
  
}
GROUP BY ?relation ?option ?headEntities ?tripleCount
HAVING (((COUNT(DISTINCT ?h)) / (xsd:float(?headEntities))) > "0.5"^^xsd:decimal)
    }
  }
     GRAPH <http://bias.org/validation-graph> {
        ?h ?relation ?t .
    }
}
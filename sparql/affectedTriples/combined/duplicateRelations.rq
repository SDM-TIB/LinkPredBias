# How many triples are affected by relations that have a jaccard score > 0.5
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT (SUM(?tripleCountR) AS ?affectedTriples) WHERE {
  GRAPH <http://www.ontotext.com/explicit> {
    SELECT ?r (?s AS ?similarRelation) ?tripleCountR ?tripleCountS ((COUNT(*)) / (xsd:float(?tripleCountR + ?tripleCountS - COUNT(*))) AS ?jaccardScore) WHERE {
    {
      SELECT ?r (COUNT(*) AS ?tripleCountR) WHERE { ?head ?r ?tail. }
      GROUP BY ?r
    }
    {
      SELECT ?s (COUNT(*) AS ?tripleCountS) WHERE { ?head ?s ?tail. }
      GROUP BY ?s
    }
        {
            SELECT ?h ?r ?s ?t WHERE {
                ?h ?r ?t;
      				?s ?t.
    			FILTER(?s != ?r)
            }
        }
    
}
GROUP BY ?r ?s ?tripleCountR ?tripleCountS
HAVING ((COUNT(*)) / (xsd:float(?tripleCountR + ?tripleCountS - COUNT(*))) > 0.5)
  }
}
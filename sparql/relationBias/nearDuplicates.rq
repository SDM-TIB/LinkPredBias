# What relations are near-duplicate to others? (based on similarityScore)
# similarityScore(r,s) is defined as the fraction of head-tail combinations in r that also occur in s 
# The metric is therefore sensitive to the order of parameters (similarityScore
# (r, s) != similarityScore(s, r))
# --> Bias type 3 in Rossi paper
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT ?relation (?s AS ?similarRelation) ((COUNT(*)) / (xsd:float(?tripleCount)) AS ?similarityScore) WHERE {
  GRAPH <http://www.ontotext.com/explicit> {
    {
      SELECT ?relation (COUNT(*) AS ?tripleCount) WHERE { ?head ?relation ?tail. }
      GROUP BY ?relation
    }
    ?h ?relation ?t;
      ?s ?t.
    FILTER(?s != ?relation)
  }
}
GROUP BY ?relation ?s ?tripleCount
HAVING (((COUNT(*)) / (xsd:float(?tripleCount))) > "0.5"^^xsd:decimal)
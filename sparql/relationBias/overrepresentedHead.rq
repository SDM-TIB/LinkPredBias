# What relations have an overrepresented head? (>50% of all head mentions for a relation)
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT ?head ?relation  (COUNT(*) AS ?filteredTripleCount) ?tripleCount (xsd:float((COUNT(*)) / (xsd:float(?tripleCount))) AS ?representation) WHERE {
  GRAPH <http://www.ontotext.com/explicit> {
    {
      SELECT ?relation (COUNT(*) AS ?tripleCount) WHERE { ?s ?relation ?o. }
      GROUP BY ?relation
    }
    ?head ?relation ?tail.
  }
}
GROUP BY ?relation ?head ?tripleCount
HAVING (((COUNT(*)) / (xsd:float(?tripleCount))) > "0.5"^^xsd:decimal)
ORDER BY DESC (?filteredTripleCount)